/**************************************************\
Name: 戰死者之墓2.0 控制腳本
Project: Bio-life Labs N Controller
Design by: ROLab jian916
Design Date: 2018-1-18
Version: 1.0
Support: rA

V1.0 2018-1-18
	完成基本架構

\**************************************************/

lhz_dun_n,0,0,0	script	mvp_lhz_dun_n	-1,{
end;
OnKillMobEvent:
	set .@count,getd(".mob_"+killedrid+"_count")+1;
	if(.@count > 110){
		switch(rand(6)){
			case 0: set .@x,40;   set .@y,210; break;
			case 1: set .@x,140;  set .@y,232; break;
			case 2: set .@x,238;  set .@y,203; break;
			case 3: set .@x,238;  set .@y,140; break;
			case 4: set .@x,140;  set .@y,65;  break;
			case 5: set .@x,30;   set .@y,140; break;
		}
		monster "lhz_dun_n",.@x,.@y,"--ja--",getd(".mob_"+killedrid+"_boss"),1,"mvp_lhz_dun_n::OnKillBossEvent";
		set .@count,0;
		if(.boss_count == 3)
			mapannounce "lhz_dun_n","死神的聲音：感受到非常強烈的氣息。",bc_map,0x00FF00;
	}
	setd(".mob_"+killedrid+"_count",.@count);
end;
OnKillBossEvent:
	if(.cooldown_time > 0) end; // 不管是不是在MVP的CD時間內, 還是會召喚BOSS, 但不觸發召喚MVP條件
	set .boss_count,.boss_count+1;
	if(.boss_count > 4){
		set .cooldown_time,1;
		set .boss_count,0;
		mapannounce "lhz_dun_n","死神的聲音：原來你不是正港的勇士。",bc_map,0xFF0000;
		set .@mvp_id,.lhz_mvp[rand(.mvp_size)];
		switch(rand(7)){
			case 0: set .@x,40;   set .@y,210; break;
			case 1: set .@x,140;  set .@y,232; break;
			case 2: set .@x,238;  set .@y,203; break;
			case 3: set .@x,238;  set .@y,140; break;
			case 4: set .@x,140;  set .@y,65;  break;
			case 5: set .@x,30;   set .@y,140; break;
			case 6: set .@x,140;  set .@y,150; break;
		}
		monster "lhz_dun_n",.@x,.@y,"--ja--",.@mvp_id,1,"mvp_lhz_dun_n::OnKillMvpEvent";
		explode(.@mvp_name$,getmonsterinfo(.@mvp_id,MOB_NAME)," "); // 只取後段名稱
		mapannounce "lhz_dun_n","死神的聲音：正港的勇士終於出現了，你好啊~"+.@mvp_name$[1]+"，好久不見啊!",bc_map,0xFF0000;
	}
end;
OnKillMvpEvent:
	mapannounce "lhz_dun_n","死神的聲音：後會有期了，曾是戰士的你喔!",bc_map,0x00FF00;
	initnpctimer; // MVP死亡進入CD時間
end;
OnTimer7200000:
	stopnpctimer;
	killmonster "lhz_dun_n","mvp_lhz_dun_n::OnKillBossEvent";
	for(set .@i,0; .@i<.mob_size; set .@i,.@i+1)
		setd(".mob_"+.lhz_mob[.@i]+"_count",0); // 重新計算普通怪計數
	set .cooldown_time,0;
	announce "死神的聲音：來吧!正港的戰士喔!",bc_all,0x00FF00;
end;
OnInit:
	setarray .lhz_mob  ,3208,3209,3210,3211,3212,3213,3226,3227,3228,3229,3230,3231,3232; // 普怪
	setarray .@lhz_boss,3214,3215,3216,3217,3218,3219,3233,3234,3235,3236,3237,3238,3239; // BOSS 同類型對應上方普怪
	setarray .lhz_mvp  ,3220,3221,3222,3223,3224,3225,3240,3241,3242,3243,3244,3245,3246; // MVP
	set .mob_size,getarraysize(.lhz_mob);
	set .mvp_size,getarraysize(.lhz_mvp);
	for(set .@i,0; .@i<.mob_size; set .@i,.@i+1)
		setd(".mob_"+.lhz_mob[.@i]+"_boss",.@lhz_boss[.@i]);
end;
}

lhz_dun_n,0,0,0,0	monster	闇●十字斬首者	3208,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●大主教	3209,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●咒術師	3210,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●遊俠	3211,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●機械工匠	3212,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●盧恩騎士	3213,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●皇家禁衛隊	3226,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●基因學者	3227,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●妖術師	3228,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●修羅	3229,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●神行太保	3230,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●宮廷樂師	3231,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"
lhz_dun_n,0,0,0,0	monster	闇●浪姬舞者	3232,20,5000,0,"mvp_lhz_dun_n::OnKillMobEvent"

lhz_dun_n	mapflag	restricted	4
lhz_dun_n	mapflag	restricted	6
lhz_dun_n	mapflag	nobranch
lhz_dun_n	mapflag	nocostume
lhz_dun_n	mapflag	noicewall
lhz_dun_n	mapflag	nomemo
// lhz_dun_n	mapflag	nosave	SavePoint
lhz_dun_n	mapflag	noteleport
lhz_dun_n	mapflag	nowarp
lhz_dun_n	mapflag	nowarpto
